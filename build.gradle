buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}


plugins {
  id "com.gradle.build-scan" version "1.9"
  id "com.matthewprenger.cursegradle" version "1.1.0"
  id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.64"
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

group= "zmaster587.advancedRocketry" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AdvancedRocketry"
sourceCompatibility = 8
targetCompatibility = 8

jar {
        manifest {
        attributes 'FMLCorePlugin': 'zmaster587.advancedRocketry.asm.AdvancedRocketryPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

apply from: "gradle/scripts/getbuildnumber.gradle"
minecraft {
    version = "1.12.2-14.23.4.2739"
    runDir = "run"
    mappings = "snapshot_20171003"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
/*    replaceIn "src/main/java/zmaster587/advancedRocketry/AdvancedRocketry.java"
    replace "@MAJOR@", version_major
    replace "@MINOR@", version_minor
    replace "@REVIS@", version_revis
    replace "@BUILD@", project.getBuildNumber()
    replace "%LIBVULPESVERSION%", project.libVulpesVersion*/
}

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

task filter(type: Copy) {
    from 'src/main/java'
    into "${buildDir}/explodedAR"
    // Substitute property tokens in files
    expand(MAJOR: project.version_major, MINOR: project.version_minor)
    expand(project.properties)
    // Use some of the filters provided by Ant
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: [MAJOR: project.version_major, MINOR: project.version_minor])
    // Use a closure to filter each line
    filter { String line ->
        "$line"
    }
    // Use a closure to remove lines
    filter { String line ->
        line.startsWith('-') ? null : line
    }
    filteringCharset = 'UTF-8'
}

apply from: "gradle/scripts/jars.gradle"
apply from: "gradle/scripts/version.gradle"
apply from: "gradle/scripts/deps.gradle"

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

curseforge{
  if (project.hasProperty("thecursedkey")) {
        apiKey = project.thecursedkey
    }
    else {
    apiKey = ""
    }
  project {
  //AdvancedRocketry
    id = "236542"
    relations {
        requiredDependency 'libvulpes'
    }
    changelog = file('changelog.html')
    changelogType = 'html'
    releaseType = "beta"
    addGameVersion minecraft.version
    mainArtifact(jar) {
      displayName = "AdvancedRocketry ${project.version} build " + project.getBuildNumber() +  " for " + minecraft.version
    }
    addArtifact (deobfJar){
    	 displayName = "AdvancedRocketry ${project.version}-deobf build " + project.getBuildNumber() + " for " + minecraft.version
    }
   }
}

// Commented out until jenkins supports newer verisons of java
task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File ("changelog.html")
    untaggedName = "Current release ${project.version}"
    
    //Get the last commit from the cache or config if no cache exists
    File newFile = rootProject.file('lasthash.txt')
    if (!newFile.exists()) fromCommit = startGitRev
    else fromCommit = newFile.text.trim()
    
    //get the latest commit and write it to the cache file
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    newFile.delete()
    newFile << stdout.toString().trim()
    
    toRef =  "HEAD"
    gitHubIssuePattern = "nonada123";
    templateContent = """
{{#tags}}
  <h3>{{name}}</h3>
  <ul>
    {{#commits}}
    <li> <a href="https://github.com/zmaster587/AdvancedRocketry/commit/{{hash}}" target=_blank> {{{message}}}</a>
</li>
    {{/commits}}
  </ul>
{{/tags}}
"""
}

tasks.build.dependsOn makeChangelog

/*buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service' 
    licenseAgree = 'yes'                                        

    publishAlways() 

    if (System.getenv('CI')) {
        tag 'CI'
    } else {
        tag 'Local'
    }

    //tag System.getProperty('os.name')                                            
}*/

